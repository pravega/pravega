/*
 * This build file was auto generated by running the Gradle 'init' task
 * by 'kandha' at '4/27/16 4:35 PM' with Gradle 2.11
 *
 * This generated file contains a commented-out sample Java project to get you started.
 * For more details take a look at the Java Quickstart chapter in the Gradle
 * user guide available at https://docs.gradle.org/2.11/userguide/tutorial_java_projects.html
 */

// Apply the java plugin to add support for Java

allprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'eclipse'
    apply plugin: 'maven'
    apply plugin: 'signing'
    apply plugin: 'checkstyle'

    checkstyle {
        toolVersion = "7.1"
    }

    repositories {
        // Use 'jcenter' for resolving your dependencies.
        // You can declare any Maven/Ivy/file repository here.
        jcenter()
        mavenCentral()
        mavenLocal()
        maven {
            url "https://raw.github.com/twitter/bookkeeper/mvn-repo/4.3.4-TWTTR"
        }
        maven {
            url "http://maven.twttr.com"
        }
    }

    checkstyle {
        configFile = new File(rootDir, "checkstyle/checkstyle.xml")
        configProperties = [importControlFile: "$rootDir/checkstyle/import-control.xml"]
    }

    dependencies {
        // The production code uses the SLF4J logging API at compile time
        compile 'org.slf4j:slf4j-api:1.7.14'
        compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.1.7'

        testCompile 'junit:junit:4.12'
        compile group: 'com.google.guava', name: 'guava', version: '19.0'
        compile group: 'commons-lang', name: 'commons-lang', version: '2.6'
        compile group: 'com.google.code.findbugs', name: 'jsr305', version: '1.3.9'
        compile group: 'org.projectlombok', name: 'lombok', version: '1.16.10'
        compile 'io.netty:netty-all:4.0.36.Final'

        testCompile "org.mockito:mockito-core:2.+"
    }

}

def pkgs = ['common', 'clients:streming', 'service:server', 'integrationtests']

project('common') {
    dependencies {
        testCompile project(':testcommon')
    }
}


project('clients:streaming') {
    dependencies {
        compile project(':common')
        compile project(":controller:contract")
        testCompile project(':testcommon')
    }
}

project('testcommon') {
    dependencies {
        compile 'junit:junit:4.12'
    }
}

project('service:contracts') {
    dependencies {
        compile project(':common')
        testCompile project(':testcommon')
    }
}

project('service:storage') {
    dependencies {
        compile project(':common')
        compile project(':service:contracts')
        testCompile project(':testcommon')
    }
}

project('service:storage:impl') {
    dependencies {
        compile('com.twitter:distributedlog-core:0.3.51-RC1') {
            exclude group: 'org.slf4j', module: 'slf4j-log4j12'
        }
        compile('com.twitter:distributedlog-client:0.3.51-RC1') {
            exclude group: 'org.slf4j', module: 'slf4j-log4j12'
        }
        compile project(':common')
        compile project(':service:storage')
        testCompile project(':testcommon')
    }
}

project('service:server') {
    dependencies {
        compile project(':common')
        compile project(':service:contracts')
        compile project(':service:storage')
        testCompile project(':testcommon')
    }
}

project('service:server:host') {
    dependencies {
        compile project(':common')
        compile project(':service:contracts')
        compile project(':service:storage')
        compile project(':service:storage:impl')
        compile project(':service:server')
        testCompile project(':testcommon')
    }
}

project('integrationtests') {
    dependencies {
        compile project(':common')
        compile project(':clients:streaming')
        compile project(':service:server')
        compile project(':service:server:host')
        testCompile project(':testcommon')
        testCompile files(project(':common').sourceSets.test.output)
        testCompile files(project(':clients:streaming').sourceSets.test.output)
        testCompile files(project(':service:server').sourceSets.test.output)
        testCompile files(project(':service:server:host').sourceSets.test.output)
    }
    task startServer(type: JavaExec) {
        main = "com.emc.pravega.service.server.host.ServiceStarter"
        classpath = sourceSets.main.runtimeClasspath
        standardInput = System.in
    }

    task startServerInteractive(type: JavaExec) {
        main = "com.emc.pravega.service.server.host.InteractiveStreamSegmentStoreTester"
        classpath = sourceSets.main.runtimeClasspath
        standardInput = System.in
    }

    task startBenchmark(type: JavaExec) {
        main = "com.emc.pravega.service.server.host.ServiceBenchmark"
        classpath = sourceSets.main.runtimeClasspath
        standardInput = System.in
    }


    task startProducer(type: JavaExec) {
        main = "com.emc.pravega.demo.StartProducer"
        classpath = sourceSets.main.runtimeClasspath
        standardInput = System.in
    }
}

project('benchmark') {
    dependencies {
        compile project(':common')
        compile project(':clients:streaming')
        compile project(':service:server')
        compile project(':service:server:host')
        testCompile project(':testcommon')
        testCompile files(project(':common').sourceSets.test.output)
        testCompile files(project(':clients:streaming').sourceSets.test.output)
        testCompile files(project(':service:server').sourceSets.test.output)
        testCompile files(project(':service:server:host').sourceSets.test.output)
    }

    task startProducer(type: JavaExec) {
        main = "com.emc.pravega.demo.StartECSProducer"
        classpath = sourceSets.main.runtimeClasspath
        standardInput = System.in
    }
}

project('controller') {
    dependencies {
        testCompile project(':testcommon')
    }
}

project('controller:contract') {

    task compileThrift(type: Exec) {
        println "Using Docker [Image: thrift:0.9.3] to compile thrift files"
        commandLine 'docker', 'run', '--rm', '-v', "$projectDir:/data", 'thrift:0.9.3', 'thrift',
                '-o', '/data/src/generated', '--gen', 'java:private-members,hashcode', '-r', '/data/src/main/thrift/Controller.thrift'
    }

    sourceSets {
        main.java.srcDirs += "$projectDir/src/generated/gen-java"
    }

    checkstyle {
        // Exclude generated code from Checkstyle checks
        sourceSets -= sourceSets.main
    }

    dependencies {
        // https://mvnrepository.com/artifact/org.apache.thrift/libthrift
        compile group: 'org.apache.thrift', name: 'libthrift', version: '0.9.3'
        compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.0'
    }
}

project('controller:server') {
    apply plugin: 'application'
    mainClassName = "com.emc.pravega.controller.server.Main"
    //applicationDefaultJvmArgs = ["-Dlog4j.configuration=conf/log4j.properties"]

    dependencies {
        compile project(':common')
        compile project(":controller:contract")
        compile project(":clients:streaming")
        compile group: 'com.typesafe', name: 'config', version: '1.3.0'
        compile group: 'org.apache.curator', name: 'curator-framework', version: '2.11.0'
        compile group: 'org.apache.curator', name: 'curator-recipes', version: '2.11.0'
        compile group: 'org.apache.curator', name: 'curator-client', version: '2.11.0'
        compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.3.2'
        testCompile group: 'org.apache.curator', name: 'curator-test', version: '2.11.0'
    }
}

allprojects {
    tasks.withType(GroovyCompile) {
        groovyOptions.fork = false
    }
    tasks.withType(Test) {
        // containers (currently) have 2 dedicated cores and 4GB of memory
        maxParallelForks = 2
        minHeapSize = '128m'
    }
}


subprojects {
    task allDeps(type: DependencyReportTask) {}
}
