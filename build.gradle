import org.gradle.internal.jvm.Jvm

/**
 * Copyright Pravega Authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
// Apply the java plugin to add support for Java
buildscript {

    // log the current JVM version.
    println "Build JVM Version is : " + Jvm.current()
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath group: 'com.google.protobuf', name:'protobuf-gradle-plugin', version: protobufGradlePlugin
        classpath "gradle.plugin.org.nosphere.apache:creadur-rat-gradle:0.3.0"
        classpath group: 'org.hidetake', name: 'gradle-ssh-plugin', version: gradleSshPluginVersion
        classpath group: 'ru.vyarus', name: 'gradle-mkdocs-plugin', version: gradleMkdocsPluginVersion
        classpath "gradle.plugin.com.github.spotbugs.snom:spotbugs-gradle-plugin:${spotbugsPluginVersion}"
        classpath "org.ajoberstar.grgit:grgit-gradle:${gradleGitPluginVersion}"
        classpath "org.ajoberstar.grgit:grgit-core:${gradleGitPluginVersion}"
        classpath "io.franzbecker:gradle-lombok:${gradleLombokPluginVersion}"
    }

    // Specifically force the version for GrGit plugin required modules to prevent downloading the most recent release.
    configurations.all {
        resolutionStrategy {
            force "org.ajoberstar.grgit:grgit-gradle:" + gradleGitPluginVersion
            force "org.ajoberstar.grgit:grgit-core:" + gradleGitPluginVersion
        }
    }
}


if (project.hasProperty("enableMkdocs") && project.property("enableMkdocs").equalsIgnoreCase("true")) {
    apply from: "$rootDir/gradle/mkdocs.gradle"
}

// apply the plugin outside of allProjects since the plugin attempts to set the grgit property for all the projects
// https://github.com/ajoberstar/grgit/blob/master/src/main/groovy/org/ajoberstar/grgit/gradle/GrgitPlugin.groovy#L27
apply plugin: 'org.ajoberstar.grgit'

allprojects {
    apply plugin: 'idea'
    apply plugin: 'eclipse'
    if (file("src/main/java").isDirectory()) {
        apply plugin: 'java'
        apply plugin: 'io.franzbecker.gradle-lombok'
        lombok {
            version = lombokVersion
        }
        dependencies {
            //These are compile time only dependencies needed accross all targets. Lombok uses them and may generate strange errors if they are missing.
            compileOnly group: 'com.github.spotbugs', name: 'spotbugs-annotations', version: spotbugsAnnotationsVersion
            testCompileOnly group: 'com.github.spotbugs', name: 'spotbugs-annotations', version: spotbugsAnnotationsVersion
            compileOnly group: 'net.jcip', name: 'jcip-annotations', version: jcipAnnotationsVersion
            testCompileOnly group: 'net.jcip', name: 'jcip-annotations', version: jcipAnnotationsVersion
            compileOnly 'org.projectlombok:lombok:' + lombokVersion
            testCompileOnly 'org.projectlombok:lombok:' + lombokVersion
            annotationProcessor 'org.projectlombok:lombok:' + lombokVersion
            testAnnotationProcessor 'org.projectlombok:lombok:' + lombokVersion
        }
        // Delombok sources.
        task delombok(type: io.franzbecker.gradle.lombok.task.DelombokTask, dependsOn: compileJava) {
            ext.outputDir = file("$buildDir/delombok")
            outputs.dir(outputDir)
            sourceSets.main.java.srcDirs.each {
                inputs.dir(it)
                args(it, "-d", outputDir)
            }
        }
    }
    // Plugin configurations
    apply from: "$rootDir/gradle/application.gradle"
    apply from: "$rootDir/gradle/checkstyle.gradle"
    apply from: "$rootDir/gradle/eclipse.gradle"
    apply from: "$rootDir/gradle/spotbugs.gradle"
    apply from: "$rootDir/gradle/idea.gradle"
    apply from: "$rootDir/gradle/jacoco.gradle"
    apply from: "$rootDir/gradle/java.gradle"
    apply from: "$rootDir/gradle/maven.gradle"
    apply from: "$rootDir/gradle/protobuf.gradle"
    apply from: "$rootDir/gradle/rat.gradle"

    repositories {
        mavenCentral()
        mavenLocal()
    }

    version = getProjectVersion()
    group = "io.pravega"

    configurations.all {
        resolutionStrategy {
            //failOnVersionConflict()
            force "com.google.guava:guava:" + guavaVersion
            force "com.google.protobuf:protobuf-java:" + protobufProtocVersion
            force "com.google.protobuf:protobuf-java-util:" + protobufProtocVersion
	    force "io.grpc:grpc-context:" + grpcVersion
            force "commons-beanutils:commons-beanutils:" + commonsBeanutilsVersion
            force "org.apache.commons:commons-compress:" + apacheCommonsCompressVersion
            force "org.apache.commons:commons-lang3:" + commonsLang3Version
            force "org.apache.curator:curator-framework:" + apacheCuratorVersion
            force "org.glassfish.jersey.core:jersey-common:" + jerseyVersion
            force "org.glassfish.jersey.core:jersey-server:" + jerseyVersion
            force "com.fasterxml.jackson.core:jackson-databind:" + jacksonVersion
            force "org.slf4j:slf4j-api:" + slf4jApiVersion
            force "org.apache.zookeeper:zookeeper:" + apacheZookeeperVersion
            force "io.netty:netty-common:" + nettyVersion
            force "io.netty:netty-transport:" + nettyVersion
            force "io.netty:netty-handler:" + nettyVersion
            force "io.netty:netty-codec:" + nettyVersion
            force "io.netty:netty-codec-dns:" + nettyVersion
            force "io.netty:netty-codec-http:" + nettyVersion
            force "io.netty:netty-codec-http2:" + nettyVersion
            force "io.netty:netty-codec-socks:" + nettyVersion
            force "io.netty:netty-handler-proxy:" + nettyVersion
            force "io.netty:netty-resolver-dns:" + nettyVersion
            force "io.netty:netty-transport-native-epoll:" + nettyVersion
            force "io.netty:netty-tcnative-boringssl-static:" + nettyBoringSSLVersion
            force "junit:junit:" + junitVersion
            force "org.bouncycastle:bcprov-jdk15on:" + bouncyCastleVersion
	    force "org.bouncycastle:bcpkix-jdk15on:" + bouncyCastleVersion
	    force "org.apache.avro:avro:" + apacheAvroVersion
	    force "org.jetbrains.kotlin:kotlin-stdlib-common:" + kotlinVersion
	    force "org.jetbrains.kotlin:kotlin-stdlib:" + kotlinVersion
	    force "org.yaml:snakeyaml:" + snakeYamlVersion
            force "com.fasterxml.woodstox:woodstox-core:" + woodstoxVersion
	    force "org.jdom:jdom2:" + jdomVersion
	    force "commons-net:commons-net:" + commonsNetVersion
            // Netty 4 uber jar
            exclude group: 'io.netty', module: 'netty-all'
            // Netty 3
            exclude group: 'io.netty', module: 'netty'
            dependencySubstitution {
                substitute module("javax.ws.rs:jsr311-api") with module("javax.ws.rs:javax.ws.rs-api:" + javaxwsrsApiVersion)
            }
        }
    }

    tasks.withType(Test) {
        jvmArgs = ["-Xmx2g"]
    }
}

project('common') {
    dependencies {
        testCompile project(':test:testcommon')
        compile group: 'commons-io', name: 'commons-io', version: commonsioVersion
        compile group: 'com.google.guava', name: 'guava', version: guavaVersion
        //Do NOT add any additional dependencies here.
    }

    tasks.withType(JavaCompile) {
        options.compilerArgs.remove(getDefaultJavaVersion())
        options.compilerArgs.add(clientJavaVersion)
    }

    javadoc {
        title = "Pravega Common Libraries"
        dependsOn delombok
        source = delombok.outputDir
        failOnError = true
    }
}

/**
 * common_server is the same as 'common' except that it is meant for server-side consumption only (Segment Store or
 * Controller). This should never be used by the Client (or any of its dependencies) since the Client must compile with
 * JDK8, while the Server may use newer versions of the JDK.
 */
project('common_server') {
    dependencies {
        compile project(':common')
        testCompile project(':test:testcommon')
        compile group: 'com.google.guava', name: 'guava', version: guavaVersion
        //Do NOT add any additional dependencies here.
    }

    javadoc {
        title = "Pravega Common Libraries for Server-Side use Only"
        dependsOn delombok
        source = delombok.outputDir
        failOnError = true
    }
}

def withoutLogger = { exclude group: 'org.slf4j', module: 'slf4j-log4j12'
    exclude group: 'org.slf4j', module: 'slf4j-simple' }

def withoutJaxbAndJjwt = { exclude group: 'javax.xml.bind', module: 'jaxb-api'
    exclude group: 'org.glassfish.jaxb', module: 'jaxb-runtime'
    exclude group: 'io.jsonwebtoken', module: 'jjwt'}

project('shared:authplugin') {
    dependencies {
        compile group: 'com.google.guava', name: 'guava', version: guavaVersion
    }

    tasks.withType(JavaCompile) {
        options.compilerArgs.remove(getDefaultJavaVersion())
        options.compilerArgs.add(clientJavaVersion)
    }

    javadoc {
        title = "Pravega Auth API"
        dependsOn delombok
        source = delombok.outputDir
        failOnError = true
    }
}

project('shared:basic-authplugin') {
    dependencies {
        compile project(':common')
        compile project(':shared:authplugin')
        compile project(':shared:security')
        testCompile project(':test:testcommon')
    }

    javadoc {
        title = "AuthHandler supporting HTTP Basic Authentication"
        dependsOn delombok
        source = delombok.outputDir
        failOnError = true
    }
}

project('shared:security') {
    dependencies {
        compile project(':common')
        compile project(':shared:authplugin')
        compile project(':shared:protocol')
        compile group: 'io.jsonwebtoken', name: 'jjwt', version: jjwtVersion

        // Adding JAXB API and the reference implementation as a dependency here, since they are not available
        // in newer Java SE versions (9 and newer).
        compile group: 'javax.xml.bind', name: 'jaxb-api', version: jaxbVersion
        compile group: 'org.glassfish.jaxb', name: 'jaxb-runtime', version: jaxbVersion
        // We need to explicitly import these as Bookkeeper 4.14+ is not transitively including them anymore.
        compile group: 'org.bouncycastle', name: 'bcprov-ext-jdk15on', version: bouncyCastleVersion

        testCompile project(':test:testcommon')
    }

    tasks.withType(JavaCompile) {
        options.compilerArgs.remove(getDefaultJavaVersion())
        options.compilerArgs.add(clientJavaVersion)
    }

    javadoc {
        exclude 'io/pravega/shared/*'
        dependsOn delombok
        source = delombok.outputDir
        failOnError = true
    }
}
project ('shared:cluster') {
    dependencies {
        compile project(':common')
        compile group: 'com.google.guava', name: 'guava', version: guavaVersion
        compile group: 'org.apache.commons', name: 'commons-lang3', version: commonsLang3Version
        compile group: 'org.apache.curator', name: 'curator-recipes', version: apacheCuratorVersion
        testCompile group: 'org.apache.curator', name: 'curator-test', version: apacheCuratorVersion
        testCompile project(':test:testcommon')
    }
    javadoc {
        exclude 'io/pravega/shared/*'
        dependsOn delombok
        source = delombok.outputDir
        failOnError = true
    }
}

project ('shared:metrics') {
    dependencies {
        compile group: 'io.micrometer', name: 'micrometer-core', version: micrometerVersion
        // https://mvnrepository.com/artifact/io.micrometer/micrometer-core/<micrometerVersion>
        compile group: 'io.micrometer', name: 'micrometer-registry-statsd', version: micrometerVersion
        // https://mvnrepository.com/artifact/io.micrometer/micrometer-registry-statsd/<micrometerVersion>
        compile group: 'io.micrometer', name: 'micrometer-registry-influx', version: micrometerVersion
        // https://mvnrepository.com/artifact/io.micrometer/micrometer-registry-influx/<micrometerVersion>
        compile group: 'io.micrometer', name: 'micrometer-registry-prometheus', version: micrometerVersion
        // https://mvnrepository.com/artifact/io.micrometer/micrometer-registry-prometheus/<micrometerVersion>
        compile project(':common')
        compile group: 'org.glassfish.jersey.containers', name: 'jersey-container-grizzly2-http', version: jerseyVersion
        runtimeOnly group: 'org.glassfish.jersey.inject', name: 'jersey-hk2', version: jerseyVersion
        compile group: 'ch.qos.logback', name: 'logback-classic', version: qosLogbackVersion
        testCompile project(':test:testcommon')
        testCompile group: 'javax.xml.bind', name: 'jaxb-api', version: jaxbVersion
    }

    javadoc {
        exclude 'io/pravega/shared/*'
        dependsOn delombok
        source = delombok.outputDir
        failOnError = true
    }
}

project('shared:protocol') {
    dependencies {
        compile project(':common')
        compile group: 'io.netty', name: 'netty-handler', version: nettyVersion
        compile group: 'io.netty', name: 'netty-tcnative-boringssl-static', version: nettyBoringSSLVersion
        compile group: 'com.google.guava', name: 'guava', version: guavaVersion
        testCompile project(':test:testcommon')
        testCompile group: 'org.slf4j', name: 'log4j-over-slf4j', version: slf4jApiVersion
        testCompile group: 'ch.qos.logback', name: 'logback-classic', version: qosLogbackVersion
        testCompile project(path: ':common', configuration: 'testRuntime')
    }

    tasks.withType(JavaCompile) {
        options.compilerArgs.remove(getDefaultJavaVersion())
        options.compilerArgs.add(clientJavaVersion)
    }
}

project ('shared:rest') {
    sourceSets {
        main.resources.srcDirs += "$projectDir/src/conf"
        test.resources.srcDirs += "$rootDir/config"
    }

    dependencies {
        compile project(':common')
        compile "io.grpc:grpc-core:" + grpcVersion
        compile project(":shared:authplugin")
        compile project(":shared:basic-authplugin")
        compile project(":shared:security")
        compile group: 'org.glassfish.jersey.containers', name: 'jersey-container-grizzly2-http', version: jerseyVersion
        compile group: 'org.glassfish.jersey.inject', name: 'jersey-hk2', version: jerseyVersion
        testCompile project(':test:testcommon')
        testCompile project(":shared:authplugin").sourceSets.test.output
        testCompile "io.grpc:grpc-netty:" + grpcVersion
        testCompile group: 'ch.qos.logback', name: 'logback-classic', version: qosLogbackVersion
    }
}

project ('shared:health') {
    dependencies {
        compile project(':common')
        testCompile project(':test:testcommon')
        compile group: 'ch.qos.logback', name: 'logback-classic', version: qosLogbackVersion
    }

    javadoc {
        exclude 'io/pravega/shared/*'
        dependsOn delombok
        source = delombok.outputDir
        failOnError = true
    }
}

project ('shared:health-bindings') {
    dependencies {
        compile project(':shared:health')
        compile project(':shared:rest')
        compile group: 'javax.servlet', name: 'javax.servlet-api', version: javaxServletApiVersion
        compile group: 'org.glassfish.jersey.media', name: 'jersey-media-json-jackson', version: jerseyVersion
        compile(group: 'io.swagger', name: 'swagger-jersey2-jaxrs', version: swaggerJersey2JaxrsVersion) {
            exclude group: 'com.google.guava', module: 'guava'
        }
        compile group: 'javax.xml.bind', name: 'jaxb-api', version: jaxbVersion
        compile group: 'org.glassfish.jaxb', name: 'jaxb-runtime', version: jaxbVersion
        testCompile project(':test:testcommon')

    }

    javadoc {
        exclude 'io/pravega/shared/*'
        dependsOn delombok
        source = delombok.outputDir
        failOnError = true
    }
}

project('client') {
    dependencies {
        compile project(':common')
        compile project(':shared:authplugin')
        compile project(':shared:protocol')
        compile project(":shared:controller-api")
        compile project(":shared:security"), withoutJaxbAndJjwt
        compile group: 'com.google.guava', name: 'guava', version: guavaVersion
        testCompile project(':test:testcommon')
        testCompile group: 'org.slf4j', name: 'log4j-over-slf4j', version: slf4jApiVersion
        testCompile group: 'ch.qos.logback', name: 'logback-classic', version: qosLogbackVersion
        // this is used to create a mock server.
        testCompile group: 'io.netty', name: 'netty-transport-native-epoll', version: nettyVersion, classifier: 'linux-x86_64'
    }

    tasks.withType(JavaCompile) {
        options.compilerArgs.remove(getDefaultJavaVersion())
        options.compilerArgs.add(clientJavaVersion)
    }

    javadoc {
        title = "Pravega Client API"
        dependsOn delombok
        source = delombok.outputDir
        failOnError = true
        exclude "**/impl/**";
    }
}

project('test:testcommon') {
    dependencies {
        compile group: 'junit', name: 'junit', version: junitVersion
        compile group: 'com.google.guava', name: 'guava', version: guavaVersion
        compile group: 'org.apache.commons', name: 'commons-lang3', version: commonsLang3Version
        compile group: 'commons-io', name: 'commons-io', version: commonsioVersion
        compile group: 'io.netty', name: 'netty-common', version: nettyVersion
        compile group: 'org.apache.curator', name: 'curator-test', version: apacheCuratorVersion, withoutLogger
        compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: jacksonVersion
    }

    tasks.withType(JavaCompile) {
        options.compilerArgs.remove(getDefaultJavaVersion())
        options.compilerArgs.add(clientJavaVersion)
    }

    javadoc {
        dependsOn delombok
        source = delombok.outputDir
        failOnError = true
    }
}

project('segmentstore:contracts') {
    dependencies {
        compile project(':common')
        compile project(':common_server')
        testCompile project(':test:testcommon')
    }
    javadoc {
        dependsOn delombok
        source = delombok.outputDir
        failOnError = true
    }
}

project('segmentstore:storage') {
    dependencies {
        compile project(':common')
        compile project(':shared:protocol')
        compile project(':segmentstore:contracts')
        compile project(':shared:metrics')
        testCompile project(':test:testcommon')
    }
    javadoc {
        dependsOn delombok
        source = delombok.outputDir
        failOnError = true
    }
}

project('segmentstore:storage:impl') {
    dependencies {
        compile project(':common')
        compile project(':segmentstore:storage')
        compile project(':shared:metrics')
        compile group: 'org.apache.curator', name: 'curator-framework', version: apacheCuratorVersion
        compile group: 'org.apache.bookkeeper', name: 'bookkeeper-server', version: bookKeeperVersion, withoutLogger
        testCompile project(':test:testcommon')
        testCompile project(path:':segmentstore:storage', configuration:'testRuntime')
        testCompile group: 'ch.qos.logback', name: 'logback-classic', version: qosLogbackVersion
    }
    javadoc {
        dependsOn delombok
        source = delombok.outputDir
        failOnError = true
    }
}

project ('bindings') {
    dependencies {
        // For HDFS
        compile group: 'org.apache.hadoop', name: 'hadoop-common', version: hadoopVersion, withoutLogger
        compile group: 'org.apache.hadoop', name: 'hadoop-hdfs', version: hadoopVersion, withoutLogger
        compile group: 'org.apache.hadoop', name: 'hadoop-hdfs-client', version: hadoopVersion, withoutLogger
        testCompile group: 'org.apache.hadoop', name: 'hadoop-minicluster', version: hadoopVersion, withoutLogger
        testCompile group: 'ch.qos.logback', name: 'logback-classic', version: qosLogbackVersion


        // Adding JAXB API and the reference implementation as dependencies here, since they are not available
        // in newer Java SE versions (11 and newer).
        compile group: 'javax.xml.bind', name: 'jaxb-api', version: jaxbVersion
        compile group: 'org.glassfish.jaxb', name: 'jaxb-runtime', version: jaxbVersion
        compile group: 'javax.activation', name: 'javax.activation-api', version: activationVersion
        testCompile group: 'io.netty', name: 'netty-codec-http', version: nettyVersion

        //For Extended S3
        compile group: 'com.emc.ecs', name: 'object-client', version: ecsObjectClientVersion, withoutLogger

        compile group: 'software.amazon.awssdk', name: 's3', version: awsSdkVersion
        compile group: 'software.amazon.awssdk', name: 'sts', version: awsSdkVersion

        compile group: 'com.azure', name: 'azure-storage-blob', version: azureSdkVersion
        // These were previously brought in as transitive dependencies of HDFS, although they were required for running
        // ExtendedS3SimpleStorageTests. But since the HDFS 3.x upgrade, these transitive dependencies are excluded,
        // so adding explicit dependencies is necessary (as well as desirable).
        implementation group: 'com.google.guava', name: 'guava', version: guavaVersion
        implementation group: 'com.google.inject.extensions', name: 'guice-assistedinject', version: guiceVersion
        implementation group: 'com.google.inject.extensions', name: 'guice-servlet', version: guiceVersion
        implementation group: 'com.google.inject', name: 'guice', version: guiceVersion
        implementation group: 'com.google.cloud', name: 'google-cloud-storage', version: gcpSdkVersion
        implementation group: 'com.google.cloud', name: 'google-cloud-nio', version: gcpCloudNioVersion

        compile project(':common')
        compile project(':segmentstore:storage')
        compile project(':shared:metrics')
        testCompile project(':test:testcommon')
        testCompile project(path: ':segmentstore:storage', configuration: 'testRuntime')
    }
    javadoc {
        dependsOn delombok
        source = delombok.outputDir
        failOnError = true
    }
}

project('segmentstore:server') {
    dependencies {
        compile project(':common')
        compile project(':common_server')
        compile project(':segmentstore:contracts')
        compile project(':segmentstore:storage')
        compile project(':shared:metrics')
        compile project(':shared:rest')
        compile project(':shared:health')
        testCompile project(':test:testcommon')
    }
    javadoc {
        dependsOn delombok
        source = delombok.outputDir
        failOnError = true
    }
}

project('segmentstore:server:host') {
    apply plugin: 'application'
    applicationName = "pravega-segmentstore"
    mainClassName = "io.pravega.segmentstore.server.host.ServiceStarter"

    startScripts {
        classpath += files('$APP_HOME/pluginlib')
        doLast {
            def scriptFile = file getUnixScript()
            scriptFile.text = scriptFile.text.replace('$APP_HOME/lib/pluginlib', '$APP_HOME/pluginlib/*')
            def winScriptFile = file getWindowsScript()
            winScriptFile.text = winScriptFile.text.replace('%APP_HOME%\\lib\\pluginlib', '%APP_HOME%\\pluginlib\\*')
        }
    }

    applicationDefaultJvmArgs = ["-server", "-Xms512m", "-XX:+HeapDumpOnOutOfMemoryError",
                                 "-Dlog.dir=PRAVEGA_APP_HOME/logs",
                                 "-Dlog.name=segmentstore",
                                 "-Dpravega.configurationFile=PRAVEGA_APP_HOME/conf/config.properties",
                                 "-Dlogback.configurationFile=PRAVEGA_APP_HOME/conf/logback.xml"]

    applicationDistribution.from('src/config') {
        into "conf"
    }

    dependencies {
        compile project(':common')
        compile project(":shared:authplugin")
        compile project(":shared:security")
        compile project(':shared:cluster')
        compile project(':segmentstore:contracts')
        compile project(':client')
        compile project(':segmentstore:storage')
        compile project(':segmentstore:storage:impl')
        compile project(':shared:health')
        compile project(':shared:rest')
        compile project(':bindings')
        compile project(':shared:health-bindings')
        compile project(':segmentstore:server')
        compile group: 'ch.qos.logback', name: 'logback-classic', version: qosLogbackVersion
        testCompile project(':test:testcommon')
        testCompile group: 'org.apache.curator', name: 'curator-test', version: apacheCuratorVersion
        testCompile project(path:':segmentstore:server', configuration:'testRuntime')
        testCompile group: 'org.apache.hadoop', name: 'hadoop-minicluster', version: hadoopVersion, withoutLogger
        testCompile project(path:':segmentstore:storage:impl', configuration:'testRuntime')
        testCompile project(path:':bindings', configuration:'testRuntime')
        testCompile group: 'commons-httpclient', name: 'commons-httpclient', version: '3.1'
        testCompile group: 'com.google.cloud', name: 'google-cloud-storage', version: gcpSdkVersion
        testCompile group: 'com.google.cloud', name: 'google-cloud-nio', version: gcpCloudNioVersion
    }

    task createAppWithGCLogging(type: CreateStartScripts) {
        applicationName = "pravega-segmentstore-withGCLogging"
        mainClassName = "io.pravega.controller.server.Main"
        defaultJvmOpts = ["-server", "-Xms512m", "-XX:+HeapDumpOnOutOfMemoryError",
                          "-XX:+PrintGCDetails", "-XX:+PrintGCDateStamps",
                          "-Xloggc:PRAVEGA_APP_HOME/logs/gc.log", "-XX:+UseGCLogFileRotation",
                          "-XX:NumberOfGCLogFiles=2", "-XX:GCLogFileSize=64m",
                          "-Dlog.dir=PRAVEGA_APP_HOME/logs",
                          "-Dlog.name=segmentstore",
                          "-Dpravega.configurationFile=PRAVEGA_APP_HOME/conf/config.properties",
                          "-Dlogback.configurationFile=PRAVEGA_APP_HOME/conf/logback.xml"]
        classpath = startScripts.classpath
        outputDir = startScripts.outputDir
    }

    applicationDistribution.into("bin") {
        from(createAppWithGCLogging)
    }

    javadoc {
        dependsOn delombok
        source = delombok.outputDir
        failOnError = true
    }
}

project('test:integration') {
    apply plugin: 'application'
    applicationName = "pravega-selftest"
    mainClassName = "io.pravega.test.integration.selftest.SelfTestRunner"

    applicationDefaultJvmArgs = ["-server", "-Xmx4g", "-XX:+HeapDumpOnOutOfMemoryError",
                                 "-Dlog.dir=PRAVEGA_APP_HOME/logs",
                                 "-Dlog.name=selftest"]

    applicationDistribution.from('src/config') {
        into "conf"
    }

    test {
        jvmArgs = ["-server", "-Xmx2g", "-XX:+HeapDumpOnOutOfMemoryError",
                   "-Dlog.dir=PRAVEGA_APP_HOME/logs",
                   "-Dlog.name=selftest"]
    }

    dependencies {
        compile project(':common')
        compile project(':client')
        compile project(':segmentstore:server')
        compile project(':segmentstore:server:host')
        compile project(':controller')
        compile project(':test:testcommon')
        compile group: 'junit', name:'junit', version: junitVersion
        compile group: 'org.apache.curator', name: 'curator-test', version: apacheCuratorVersion
        testCompile group: 'ch.qos.logback', name: 'logback-classic', version: qosLogbackVersion
        testCompile group: 'org.apache.commons', name: 'commons-csv', version: apacheCommonsCsvVersion
        testCompile project(path:':common', configuration:'testRuntime')
        testCompile project(path:':shared:protocol', configuration:'testRuntime')
        testCompile project(path:':segmentstore:server', configuration:'testRuntime')
        testCompile project(path:':segmentstore:server:host', configuration:'testRuntime')
        testCompile project(path:':shared:metrics', configuration:'testRuntime')

        // Workaround for intellij issue, since we cannot add both the compile dependency and the testRuntime
        // dependency of the client project into the compile scope of the integration tests
        compile files(project(':client').sourceSets.test.output)
    }

    task startServer(type: JavaExec) {
        main = "io.pravega.segmentstore.server.host.ServiceStarter"
        classpath = sourceSets.main.runtimeClasspath
        standardInput = System.in
    }

    task startBenchmark(type: JavaExec) {
        main = "io.pravega.segmentstore.server.host.ServiceBenchmark"
        classpath = sourceSets.main.runtimeClasspath
        standardInput = System.in
    }

    task startLocalService(type: JavaExec) {
        main = "io.pravega.test.integration.demo.StartLocalService"
        classpath = sourceSets.main.runtimeClasspath
        standardInput = System.in
    }

    task startWriter(type: JavaExec) {
        main = "io.pravega.test.integration.demo.StartWriter"
        classpath = sourceSets.main.runtimeClasspath
        standardInput = System.in
    }

    task startReader(type: JavaExec) {
        main = "io.pravega.test.integration.demo.StartReader"
        classpath = sourceSets.main.runtimeClasspath
        standardInput = System.in
    }

    task startController(type: JavaExec) {
        main = "io.pravega.controller.server.Main"
        classpath = sourceSets.main.runtimeClasspath
        standardInput = System.in
    }

    task startScaleTest(type: JavaExec) {
        main = "io.pravega.test.integration.demo.ScaleTest"
        classpath = sourceSets.main.runtimeClasspath
        standardInput = System.in
    }

    task startEndToEndTransactionTest(type: JavaExec) {
        main = "io.pravega.test.integration.demo.EndToEndTransactionTest"
        classpath = sourceSets.main.runtimeClasspath
        standardInput = System.in
    }

    task startEventProcessorTest(type: JavaExec) {
        main = "io.pravega.test.integration.demo.EventProcessorTest"
        classpath = sourceSets.main.runtimeClasspath
        standardInput = System.in
    }

    task selftest(type: JavaExec) {
        main = "io.pravega.test.integration.selftest.SelfTestRunner"
        classpath = sourceSets.main.runtimeClasspath
        standardInput = System.in
        systemProperties System.getProperties()
    }

    task compatibilityCheck(type: JavaExec){
        main = "io.pravega.test.integration.compatibility.CompatibilityChecker"
        classpath = sourceSets.main.runtimeClasspath
        standardInput = System.in
        systemProperties System.getProperties()
    }

    /**
     * See: https://github.com/pravega/pravega/wiki/Pravega-User-CLI.
     * This will only work well from the console if all Gradle output is suppressed: -q --console=plain
     */
    task startInteractiveDemo(type: JavaExec) {
        main = "io.pravega.test.integration.demo.interactive.InteractiveDemo"
        classpath = sourceSets.main.runtimeClasspath
        standardInput = System.in
        systemProperties System.getProperties()
        systemProperties 'logback.configurationFile' : new File("${project.rootDir}/config/logback.xml")
    }
}

project('shared:controller-api') {
    apply plugin: 'com.google.protobuf'

    dependencies {
        compile project(':common')
        compile group: 'org.apache.commons', name: 'commons-lang3', version: commonsLang3Version
        compile group: 'commons-io', name: 'commons-io', version: commonsioVersion
        compile "io.grpc:grpc-auth:" + grpcVersion
        // Since Java 10 javax.annotation is no longer bundled with the JRE
        compileOnly group: 'javax.annotation', name: 'javax.annotation-api', version: javaxAnnotationVersion
    }

    tasks.withType(JavaCompile) {
        options.compilerArgs.remove(getDefaultJavaVersion())
        options.compilerArgs.add(clientJavaVersion)
    }

    javadoc {
        exclude 'io/pravega/controller/*'
        dependsOn delombok
        source = delombok.outputDir
        failOnError = true
    }
}

project('controller') {
    sourceSets {
        main.resources.srcDirs += "$projectDir/src/conf"
        test.resources.srcDirs += "$rootDir/config"
    }

    apply plugin: 'application'
    applicationName = "pravega-controller"
    mainClassName = "io.pravega.controller.server.Main"
    applicationDefaultJvmArgs = ["-server", "-Xms128m", "-XX:+HeapDumpOnOutOfMemoryError",
                                 "-Dconfig.file=PRAVEGA_APP_HOME/conf/controller.config.properties",
                                 "-Dlogback.configurationFile=PRAVEGA_APP_HOME/conf/logback.xml",
                                 "-Dlog.dir=PRAVEGA_APP_HOME/logs",
                                 "-Dlog.name=controller"]
    startScripts {
        classpath += files('$APP_HOME/pluginlib')
        doLast {
            def scriptFile = file getUnixScript()
            scriptFile.text = scriptFile.text.replace('$APP_HOME/lib/pluginlib', '$APP_HOME/pluginlib/*')
            def winScriptFile = file getWindowsScript()
            winScriptFile.text = winScriptFile.text.replace('%APP_HOME%\\lib\\pluginlib', '%APP_HOME%\\pluginlib\\*')
        }
    }
    applicationDistribution.from('src/conf') {
        into "conf"
        rename "application.conf", "controller.conf"
    }

    applicationDistribution.into('') {
        def pluginDirBase = new File(System.getProperty("java.io.tmpdir", "/tmp") + "/dummy-dir")
        pluginDirBase.mkdirs()
        def logDir = new File(pluginDirBase.absolutePath + '/pluginlib')
        logDir.mkdirs()

        from {pluginDirBase}
    }

    dependencies {
        compile project(':common')
        compile project(":shared:authplugin")
        compile project(":shared:basic-authplugin")
        compile project(":shared:security")
        compile project(":shared:controller-api")
        compile project(':shared:cluster')
        compile project(":client")
        compile project(":shared:metrics")
        compile project(":shared:rest")
        compile project(":shared:health")
        compile project(":shared:health-bindings")
        runtimeOnly group: 'ch.qos.logback', name: 'logback-classic', version: qosLogbackVersion
        compile group: 'javax.servlet', name: 'javax.servlet-api', version: javaxServletApiVersion
        compile(group: 'io.swagger', name : 'swagger-jersey2-jaxrs', version :swaggerJersey2JaxrsVersion) {
            exclude group: 'com.google.guava', module: 'guava'
        }
        compile group: 'org.apache.curator', name: 'curator-framework', version: apacheCuratorVersion
        compile group: 'org.apache.curator', name: 'curator-recipes', version: apacheCuratorVersion
        compile group: 'org.apache.curator', name: 'curator-client', version: apacheCuratorVersion
        compile group: 'org.apache.commons', name: 'commons-lang3', version: commonsLang3Version
        compile group: 'org.glassfish.jersey.media', name: 'jersey-media-json-jackson', version: jerseyVersion
        testCompile project(':test:testcommon')
        testCompile project(':shared:basic-authplugin')
        testCompile project(":shared:authplugin").sourceSets.test.output
        testCompile group: 'org.apache.curator', name: 'curator-test', version: apacheCuratorVersion
        compile group: 'io.netty', name: 'netty-tcnative-boringssl-static', version: nettyBoringSSLVersion
        // since Java 10 JAXB is not more bundled with the JRE
        compile group: 'javax.xml.bind', name: 'jaxb-api', version: jaxbVersion
        compile group: 'org.glassfish.jaxb', name: 'jaxb-runtime', version: jaxbVersion
        compile group: 'com.google.code.gson', name: 'gson', version: gsonVersion
    }

    task createAppWithGCLogging(type: CreateStartScripts) {
        applicationName = "pravega-controller-withGCLogging"
        mainClassName = "io.pravega.controller.server.Main"
        defaultJvmOpts = ["-server", "-Xms128m", "-XX:+HeapDumpOnOutOfMemoryError",
                          "-XX:+PrintGCDetails", "-XX:+PrintGCDateStamps",
                          "-Xloggc:PRAVEGA_APP_HOME/logs/gc.log", "-XX:+UseGCLogFileRotation",
                          "-XX:NumberOfGCLogFiles=2", "-XX:GCLogFileSize=64m",
                          "-Dconfig.file=PRAVEGA_APP_HOME/conf/controller.config.properties",
                          "-Dlogback.configurationFile=PRAVEGA_APP_HOME/conf/logback.xml",
                          "-Dlog.dir=PRAVEGA_APP_HOME/logs",
                          "-Dlog.name=controller"]
        classpath = startScripts.classpath
        outputDir = startScripts.outputDir
    }

    applicationDistribution.into("bin") {
        from(createAppWithGCLogging)
    }
}

project('cli:admin') {
    apply plugin: 'application'
    applicationName = "pravega-admin"
    mainClassName = "io.pravega.cli.admin.AdminCLIRunner"

    applicationDefaultJvmArgs = ["-Dlogback.configurationFile=PRAVEGA_APP_HOME/conf/admin-cli-logback.xml",
                                 "-Dlog.dir=PRAVEGA_APP_HOME/logs",
                                 "-Dlog.name=adminCli"]

    startScripts {
        classpath += files('$APP_HOME/pluginlib')
        doLast {
            def scriptFile = file getUnixScript()
            scriptFile.text = scriptFile.text.replace('$APP_HOME/lib/pluginlib', '$APP_HOME/pluginlib/*')
            def winScriptFile = file getWindowsScript()
            winScriptFile.text = winScriptFile.text.replace('%APP_HOME%\\lib\\pluginlib', '%APP_HOME%\\pluginlib\\*')
        }
    }

    dependencies {
        compile project(':common')
        compile project(':controller')
        compile project(':segmentstore:server')
        compile project(':segmentstore:storage')
        compile project(':segmentstore:server:host')
        compile project(':segmentstore:storage:impl')
        compile project(':shared:cluster')
        compile project(":shared:security")
        compile project(':test:testcommon')
        compile project(':test:integration')
        compile group: 'org.apache.bookkeeper', name: 'bookkeeper-common', version: bookKeeperVersion
        compile group: 'org.apache.curator', name: 'curator-framework', version: apacheCuratorVersion
        compile group: 'ch.qos.logback', name: 'logback-classic', version: qosLogbackVersion
        testCompile group: 'junit', name: 'junit', version: '4.12'
        testCompile group: 'org.apache.bookkeeper', name: 'bookkeeper-common', version: bookKeeperVersion, classifier: 'tests'
        testCompile group: 'org.apache.bookkeeper', name: 'bookkeeper-server', version: bookKeeperVersion, classifier: 'tests'
        testCompile group: 'org.apache.zookeeper', name: 'zookeeper', version: apacheZookeeperVersion, classifier: 'tests'
    }

    javadoc {
        dependsOn delombok
        source = delombok.outputDir
        failOnError = true
    }
}

project('cli:user') {
    apply plugin: 'application'
    applicationName = "pravega-cli"
    mainClassName = "io.pravega.cli.user.UserCLIRunner"

    startScripts {
        classpath += files('$APP_HOME/pluginlib')
        doLast {
            def scriptFile = file getUnixScript()
            scriptFile.text = scriptFile.text.replace('$APP_HOME/lib/pluginlib', '$APP_HOME/pluginlib/*')
            def winScriptFile = file getWindowsScript()
            winScriptFile.text = winScriptFile.text.replace('%APP_HOME%\\lib\\pluginlib', '%APP_HOME%\\pluginlib\\*')
        }
    }

    dependencies {
        compile project(':common')
        compile project(':client')
        compile project(':shared:protocol')
        compile project(':test:testcommon')
        compile group: 'ch.qos.logback', name: 'logback-classic', version: qosLogbackVersion
        compile group: 'com.google.code.gson', name: 'gson', version: gsonVersion
        testCompile project(':test:integration')
        testCompile group: 'junit', name: 'junit', version: '4.12'
    }

    javadoc {
        dependsOn delombok
        source = delombok.outputDir
        failOnError = true
    }
}


project('standalone') {
    apply plugin: 'application'
    applicationName = "pravega-standalone"
    mainClassName = "io.pravega.local.LocalPravegaEmulator"
    applicationDefaultJvmArgs = ["-server", "-Xmx4g", "-XX:+HeapDumpOnOutOfMemoryError",
                                 "-Dlogback.configurationFile=PRAVEGA_APP_HOME/conf/standalone-logback.xml",
                                 "-Dsinglenode.configurationFile=PRAVEGA_APP_HOME/conf/standalone-config.properties",
                                 "-Dlog.dir=PRAVEGA_APP_HOME/logs",
                                 "-Dlog.name=pravega",
                                 "-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=127.0.0.1:+JDWP_DEFAULT_PORT"]

    tasks.withType(CreateStartScripts) {
        def tplName = 'unixApplicationStartupScript.txt'
        unixStartScriptGenerator.template = resources.text.fromFile(tplName)
    }

    startScripts {
        classpath += files('$APP_HOME/pluginlib')
        doLast {
            def scriptFile = file getUnixScript()
            scriptFile.text = scriptFile.text.replace('$APP_HOME/lib/pluginlib', '$APP_HOME/pluginlib/*')
            def winScriptFile = file getWindowsScript()
            winScriptFile.text = winScriptFile.text.replace('%APP_HOME%\\lib\\pluginlib', '%APP_HOME%\\pluginlib\\*')
        }
    }

    dependencies {
        runtimeOnly group: 'ch.qos.logback', name: 'logback-classic', version: qosLogbackVersion

        compile project(':common')
        compile project(':client')
        compile project(':segmentstore:server')
        compile project(':segmentstore:server:host')
        compile project(':controller')
        compile project(':segmentstore:contracts')
        compile project(':segmentstore:storage')
        compile project(':segmentstore:storage:impl')
        compile project(':bindings')

        compile group: 'org.glassfish.jersey.containers', name: 'jersey-container-grizzly2-http', version: jerseyVersion
        compile group: 'javax.ws.rs', name: 'javax.ws.rs-api', version: javaxwsrsApiVersion
        compile group: 'org.apache.curator', name: 'curator-test', version: apacheCuratorVersion
        // https://mvnrepository.com/artifact/org.apache.hadoop/hadoop-common
        compile group: 'org.apache.hadoop', name: 'hadoop-common', version: hadoopVersion, withoutLogger
        compile group: 'org.apache.hadoop', name: 'hadoop-hdfs', version: hadoopVersion, withoutLogger
        compile group: 'org.apache.hadoop', name: 'hadoop-minicluster', version: hadoopVersion, withoutLogger
        compile group: 'io.netty', name: 'netty-tcnative-boringssl-static', version: nettyBoringSSLVersion
        compile group: 'io.jsonwebtoken', name: 'jjwt', version: jjwtVersion
        testCompile project(':test:testcommon')
        testCompile project(path:':common', configuration:'testRuntime')
        testCompile project(path:':segmentstore:server', configuration:'testRuntime')
        testCompile project(path:':segmentstore:server:host', configuration:'testRuntime')
    }

    configurations {
        runtime.exclude group: "com.sun.jersey", module: "jersey-core"
        runtime.exclude group: "com.sun.jersey", module: "jersey-server"
    }

    task startStandalone(type: JavaExec) {
        main = "io.pravega.local.LocalPravegaEmulator"
        classpath = sourceSets.main.runtimeClasspath
        systemProperties System.getProperties()
        systemProperties 'logback.configurationFile' : new File("${project.rootDir}/config/standalone-logback.xml")
        systemProperties 'singlenode.configurationFile' : new File("${project.rootDir}/config/standalone-config.properties")

        if (systemProperties.get("extDirs") != null) {
            classpath += files(systemProperties.get("extDirs"))
        }

        def JDWP_DEFAULT_PORT = 8050
        def jvmDebugPort = System.getenv("JDWP_PORT")
        if (jvmDebugPort != null && jvmDebugPort != "") {
            def matcher = jvmDebugPort =~ /^([0-9]{1,4}|[1-5][0-9]{4}|6[0-4][0-9]{3}|65[0-4][0-9]{2}|655[0-2][0-9]|6553[0-5])$/
            if (matcher.matches()) {
                JDWP_DEFAULT_PORT = jvmDebugPort
            } else {
                println "Invalid JDWP_PORT number - " +jvmDebugPort+ ", staring Pravega standalone using default port 8050"
            }

        }
        jvmArgs "-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=127.0.0.1:" + JDWP_DEFAULT_PORT

    }
}

project('test:system') {
    // Specifically publish this test project
    apply plugin: 'maven'

    // override the compile java options to disable -Werror this will remove once https://github.com/pravega/pravega/issues/3152 is resolved.
    tasks.withType(JavaCompile) {
        options.compilerArgs.remove("-Werror")
        options.compilerArgs.remove(getDefaultJavaVersion())
        options.compilerArgs.add(clientJavaVersion)
    }

    dependencies {
        // https://mvnrepository.com/artifact/com.mesosphere/marathon-client
        runtimeOnly group: 'ch.qos.logback', name: 'logback-classic', version: qosLogbackVersion
        compile group: 'com.mesosphere', name: 'marathon-client', version: marathonClientVersion, withoutLogger
        if (System.getProperty("customClient") != null) {
            implementation fileTree(System.getProperty("customClient")) { include '*.jar' }
        } else {
            compile project(":client")
        }
        compile project(":test:testcommon")
        compile group: 'junit', name: 'junit', version: junitVersion
        compile group: 'javax.ws.rs', name: 'javax.ws.rs-api', version: javaxwsrsApiVersion
        compile group: 'org.glassfish.jersey.core', name: 'jersey-client', version: jerseyVersion
        compile group: 'org.glassfish.jersey.media', name: 'jersey-media-json-jackson', version: jerseyVersion
        compile group: 'org.glassfish.jersey.connectors', name:'jersey-apache-connector', version:jerseyVersion
        compile group: 'com.spotify', name: 'docker-client', version: dockerClientVersion
        compile group: 'io.kubernetes', name: 'client-java', version: k8ClientVersion
        compile group: 'org.apache.curator', name: 'curator-framework', version: apacheCuratorVersion
        runtimeOnly group: 'org.apache.curator', name: 'curator-framework', version: apacheCuratorVersion
        runtimeOnly group: 'org.glassfish.jersey.inject', name: 'jersey-hk2', version: jerseyVersion
    }
    //disable the default test task.
    test {
        exclude 'io/pravega/**'
    }

    jar { //create a fat jar
        archiveFileName = "test-collection.jar"
        from {
            (configurations.runtimeClasspath).collect {
                it.isDirectory() ? it : zipTree(it)
            }
        }
        from sourceSets.test.output
        exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'
    }

    task testJarForDocker(type: Jar) {
        archiveFileName = "test-docker-collection.jar"
        from {
            (configurations.runtime).collect {
                it.isDirectory() ? it : zipTree(it)
            }
        }
        from sourceSets.test.output
        from sourceSets.main.output
        exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'
    }

    //This is  used to invoke systemTests
    task startSystemTests(type: Test) {
        tasks.withType(Exec) { environment "tlsEnabled", System.getProperty("tlsEnabled", "false") }
        ext.dockerRegistryUrl = project.hasProperty("dockerRegistryUrl") ? project.dockerRegistryUrl : ""
        ext.repoUrl = project.hasProperty("repoUrl") ? project.repoUrl : ""

        (includes, excludes) = subsetSystemTests()

        description 'Used to invoke system tests, example usage: gradle startSystemTests -DmasterIP=xx.xx.xx.xx'
        testClassesDirs = sourceSets.test.output.classesDirs
        classpath = sourceSets.test.runtimeClasspath

        systemProperty "test_collection_jar_path", "$jar.archivePath.path"
        systemProperty "execType", System.getProperty("execType")
        systemProperty "masterIP", System.getProperty("masterIP")
        systemProperty "imageVersion", System.getProperty("imageVersion")
        systemProperty "skipServiceInstallation", System.getProperty("skipServiceInstallation")
        systemProperty "segmentStoreExtraEnv", System.getProperty("segmentStoreExtraEnv")

        if (System.getProperty("execType") == null) {
            systemProperty "execType", "REMOTE_SEQUENTIAL"
        }
        if (System.getProperty("masterIP") == null) {
            systemProperty "masterIP", "INVALID_MASTER_IP"
        }
        if (System.getProperty("imageVersion") == null) {
            systemProperty "imageVersion", "INVALID_IMAGE_VERSION"
        }
        if (System.getProperty("skipServiceInstallation") == null) {
            //skipServiceInstallation by default.
            systemProperty "skipServiceInstallation", "true"
        }
        maxParallelForks = 1
        systemProperty "dockerImageRegistry", "${dockerRegistryUrl}"
        systemProperty "testVersion", pravegaVersion
        systemProperty "tlsEnabled", System.getProperty("tlsEnabled", "false")
        systemProperty "testArtifactUrl", "${repoUrl}/io/pravega/pravega-test-system/" +
                pravegaVersion+"/pravega-test-system-"+ pravegaVersion + ".jar"

        onlyIf { dockerRegistryUrl && repoUrl }
    }

    task copyTestsToK8sCluster(type: Exec) {
        description 'Used to copy test artifacts to the Kubernetes Cluster.'
        dependsOn 'jar'

        environment "dockerRegistryUrl", System.getProperty("dockerRegistryUrl")
        environment "imagePrefix", System.getProperty("imagePrefix", "pravega")
        environment "tier2Type", System.getProperty("tier2Type", "nfs")
        environment "pravegaOperatorChartVersion", System.getProperty("pravegaOperatorChartVersion", "0.6.1")
        environment "bookkeeperOperatorChartVersion", System.getProperty("bookkeeperOperatorChartVersion", "0.2.1")
        environment "zookeeperOperatorChartVersion", System.getProperty("zookeeperOperatorChartVersion", "0.2.13")
        environment "pravegaOperatorVersion", System.getProperty("pravegaOperatorVersion", "0.5.5")
        environment "bookkeeperOperatorVersion", System.getProperty("bookkeeperOperatorVersion", "0.1.6")
        environment "zookeeperOperatorVersion", System.getProperty("zookeeperOperatorVersion", "0.2.13")
        environment "pravegaOperatorImageName", System.getProperty("pravegaOperatorImageName", "pravega-operator")
        environment "bookkeeperOperatorImageName", System.getProperty("bookkeeperOperatorImageName", "bookkeeper-operator")
        environment "zookeeperOperatorImageName", System.getProperty("zookeeperOperatorImageName", "zookeeper-operator")
        // The below properties are used to to specify the pravega published chart and repository to deploy pravega,bookkeeper & zookkeeper operators using helm docker image properties
        environment "publishedChartName", System.getProperty("publishedChartName", "pravega")
        environment "helmRepository", System.getProperty("helmRepository", "https://charts.pravega.io")
        environment "helmHookImageName", System.getProperty("helmHookImageName", "lachlanevenson/k8s-kubectl")
        environment "tlsEnabled", System.getProperty("tlsEnabled", "false")
        environment "skipServiceInstallation", System.getProperty("skipServiceInstallation", "false")
        environment "testPodImage", System.getProperty("testPodImage", "openjdk:8u181-jre-alpine")

        commandLine './kubernetes/setupTestPod.sh'
    }

    task runFluentBitInstall(type: Exec) {
        if (System.getProperty("skipFluentInstall", "false") == "false") {
            environment 'FLUENT_BIT_STORAGE_CLASS', System.getProperty("logStorageClass", "")
            environment 'FLUENT_BIT_HOST_LOGS_PATH', System.getProperty("k8HostLogPath", "")
            environment 'ALPINE_IMAGE', System.getProperty("alpineImage", "alpine:latest")
            commandLine './kubernetes/fluentBitSetup.sh', 'install'
        } else {
            commandLine 'echo', 'Skipping Fluent-Bit installation.'
        }
    }

    task fetchSystemTestLogs(type: Exec) {
        environment 'FLUENT_BIT_EXPORT_PATH', "logs"
        commandLine './kubernetes/fluentBitSetup.sh', 'fetch-namespace-logs'
        commandLine './kubernetes/fluentBitSetup.sh', 'fetch-system-test-logs'
    }

    task startK8SystemTests(type: Test) {
        dependsOn 'copyTestsToK8sCluster', 'runFluentBitInstall' // ensure test artifacts are copied to the cluster.
        description 'Used to invoke K8s based System Tests, example usage: gradle startK8SystemTests'

        testClassesDirs = sourceSets.test.output.classesDirs
        classpath = sourceSets.test.runtimeClasspath

        (includes, excludes) = subsetSystemTests()

        ext.repoUrl = project.hasProperty("repoUrl") ? project.repoUrl : ""
        systemProperty "execType", "KUBERNETES"
        // The below properties are used to to specify the pravega docker image properties
        // e.g: private-docker-repo/pravega-prefix/pravega:version
        if (System.getProperty("dockerRegistryUrl") != null) { // docker images are pulled from dockerhub if dockerRegistryUrl is not specified.
            systemProperty "dockerRegistryUrl", System.getProperty("dockerRegistryUrl") + "/"
        }
        systemProperty "skipServiceInstallation", System.getProperty("skipServiceInstallation", "false")
        systemProperty "skipLogDownload", System.getProperty("skipLogDownload", "false")
        systemProperty "imagePrefix", System.getProperty("imagePrefix", "pravega")  // the default value of imagePrefix is pravega.
        systemProperty "pravegaImageName", System.getProperty("pravegaImageName", "pravega") // pravega image name
        systemProperty "bookkeeperImageName", System.getProperty("bookkeeperImageName", "bookkeeper") // bookkeeper image name
        systemProperty "zookeeperImageName", System.getProperty("zookeeperImageName", "zookeeper") // zookkeeper image name
        systemProperty "zookeeperImageVersion", System.getProperty("zookeeperImageVersion", "latest") // zookeeper image version
        systemProperty "imageVersion", System.getProperty("imageVersion") // pravega version.
        // bookkeeper version can be different from pravega version.
        systemProperty "bookkeeperImageVersion", System.getProperty("bookkeeperImageVersion", "imageVersion") // // bookkeeper image version
        // tier2Type , default is NFS.
        systemProperty "tier2Type", System.getProperty("tier2Type", "nfs")
        // tier2 configuration, specified as comma separated key values k1=v1,k2=v2
        systemProperty "tier2Config", System.getProperty("tier2Config")
        systemProperty "tier2Env", System.getProperty("tier2Env")
        systemProperty "pravegaOperatorChartVersion", System.getProperty("pravegaOperatorChartVersion", "0.6.1")
        systemProperty "bookkeeperOperatorChartVersion", System.getProperty("bookkeeperOperatorChartVersion", "0.2.1")
        systemProperty "zookeeperOperatorChartVersion", System.getProperty("zookeeperOperatorChartVersion", "0.2.13")
        systemProperty "pravegaOperatorVersion", System.getProperty("pravegaOperatorVersion", "0.5.5")
        systemProperty "bookkeeperOperatorVersion", System.getProperty("bookkeeperOperatorVersion", "0.1.6")
        systemProperty "zookeeperOperatorVersion", System.getProperty("zookeeperOperatorVersion", "0.2.13")
        systemProperty "pravegaOperatorImageName", System.getProperty("pravegaOperatorImageName", "pravega-operator")
        systemProperty "bookkeeperOperatorImageName", System.getProperty("bookkeeperOperatorImageName", "bookkeeper-operator")
        systemProperty "zookeeperOperatorImageName", System.getProperty("zookeeperOperatorImageName", "zookeeper-operator")
        systemProperty "publishedChartName", System.getProperty("publishedChartName", "pravega")
        systemProperty "helmRepository", System.getProperty("helmRepository", "pravega")
        systemProperty "helmHookImageName", System.getProperty("helmHookImageName", "lachlanevenson/k8s-kubectl")

        // target versions wrt Upgrade for all operators & clusters
        systemProperty "controllerLabel", System.getProperty("controllerLabel", "pravega-controller");
        systemProperty "segmentstoreLabel", System.getProperty("segmentstoreLabel", "pravega-segmentstore");
        systemProperty "tlsSecretName", System.getProperty("tlsSecretName", "selfsigned-cert-tls");
        systemProperty "bookkeeperLabel", System.getProperty("bookkeeperLabel", "bookie");
        systemProperty "pravegaID", System.getProperty("pravegaID", "pravega");
        systemProperty "bookkeeperID", System.getProperty("bookkeeperID", "pravega-bk");
        systemProperty "bookkeeperConfigMap", System.getProperty("bookkeeperConfigMap", "bk-config-map");

        systemProperty "targetPravegaOperatorVersion", System.getProperty("targetPravegaOperatorVersion", "latest")
        systemProperty "targetBookkeeperOperatorVersion", System.getProperty("targetBookkeeperOperatorVersion", "latest")
        systemProperty "targetZookeeperOperatorVersion", System.getProperty("targetZookeeperOperatorVersion", "latest")
        systemProperty "targetPravegaVersion", System.getProperty("targetPravegaVersion", "latest")
        systemProperty "targetBookkeeperVersion", System.getProperty("targetBookkeeperVersion", "latest")
        systemProperty "targetZookeeperVersion", System.getProperty("targetZookeeperVersion", "latest")
        //testPodImage , default is openjdk:8u181-jre-alpine
        systemProperty "testPodImage", System.getProperty("testPodImage", "openjdk:8u181-jre-alpine")
        systemProperty "testServiceAccount", System.getProperty("testServiceAccount", "test-framework");
        systemProperty "testClusterRoleBinding", System.getProperty("testClusterRoleBinding", "cluster-admin-testFramework");

        if (System.getProperty("imageVersion") == null) {
            systemProperty "imageVersion", "INVALID_IMAGE_VERSION"
        }
        // validating that bookkeeper operator version >= 0.1.5
        if (System.getProperty("bookkeeperOperatorVersion") ==~ /^(0)\.(1)\.(0|1|2|3|4)(-.+)*/) {
            throw new InvalidUserDataException('Requires Bookkeeper Operator Version >= 0.1.5')
        }
        // validating that pravega operator version >= 0.5.4
        if (System.getProperty("pravegaOperatorVersion") ==~ /^((0)\.(1|2|3|4)\.(\d)(-.+)*)|((0)\.(5)\.(0|1|2|3)(-.+)*)/) {
            throw new InvalidUserDataException('Requires Pravega Operator Version >= 0.5.4')
        }

        systemProperty "securityEnabled", System.getProperty("securityEnabled", "false")
        systemProperty "tlsEnabled", System.getProperty("tlsEnabled", "false")
        systemProperty "log.level", System.getProperty("log.level", "DEBUG")

        if (System.getProperty("configFile") != null) {
            systemProperty "configs", new File(System.getProperty("configFile")).text
        }
        maxParallelForks = 1

        if (System.getProperty("failFast", "false") == "true") {
            failFast = true
        }
    }

    task execShellScript(type: Exec) {

        ext.awsExecution = project.hasProperty('awsExecution') ? project.awsExecution : 'false'

        if(ext.getProperty('awsExecution').toString().equals("true")) {
            commandLine './aws/preTestScript.sh'
            args "$aws_access_key", "$aws_secret_key", "$aws_region", "$aws_key_name", "$cred_path", "$config_path", "$pravega_org", "$pravega_branch"
        } else if (project.hasProperty('CLUSTER_NAME')) {
            commandLine "./preTestScript.sh"
            args "$CLUSTER_NAME", "$MASTER", "$NUM_SLAVES"
        }
    }

    String variable

    task setAwsMasterIp(type: Exec) {

        commandLine 'echo', 'setting master ip for system tests execution on AWS'

        doLast {
            def masterIpStdOut = new ByteArrayOutputStream()
            exec {
                workingDir "$rootDir/test/system/aws"
                executable = "terraform"
                args "output", "ip"
                standardOutput = masterIpStdOut
            }

            variable = masterIpStdOut.toString()
        }
    }


    //This is  used to invoke docker based systemTests
    task startSystemTestsWithDocker(type: Test) {

        tasks.withType(Exec) { environment "tlsEnabled", System.getProperty("tlsEnabled", "false") }
        dependsOn 'testJarForDocker', 'execShellScript'

        if (System.getProperty("awsExec").equals("true")) {
            setAwsMasterIp.outputs.upToDateWhen { false }
            dependsOn 'setAwsMasterIp'
        }

        doFirst {
            systemProperty "awsMasterIP", variable
        }

        ext.dockerRegistryUrl = project.hasProperty("dockerRegistryUrl") ? project.dockerRegistryUrl : ""

        description 'Used to invoke docker based system tests, example usage: gradle startSystemTests -DmasterIP=xx.xx.xx.xx'
        testClassesDirs = sourceSets.test.output.classesDirs
        classpath = sourceSets.test.runtimeClasspath

        systemProperty "execType", System.getProperty("execType")
        systemProperty "masterIP", System.getProperty("masterIP")
        systemProperty "imageVersion", System.getProperty("imageVersion")
        systemProperty "skipServiceInstallation", System.getProperty("skipServiceInstallation")
        systemProperty "segmentStoreExtraEnv", System.getProperty("segmentStoreExtraEnv")
        systemProperty "awsExec", System.getProperty("awsExec")
        systemProperty "pravegaImageName", System.getProperty("pravegaImageName", "pravega") // pravega image name
        systemProperty "bookkeeperImageName", System.getProperty("bookkeeperImageName", "bookkeeper") // bookkeeper image name
        systemProperty "zookeeperImageName", System.getProperty("zookeeperImageName", "zookeeper") // zookkeeper image name
        systemProperty "zookeeperImageVersion", System.getProperty("zookeeperImageVersion", "latest") // zookeeper image version
        systemProperty "imagePrefix", System.getProperty("imagePrefix", "pravega")  // the default value of imagePrefix is pravega.

        if (System.getProperty("execType") == null) {
            systemProperty "execType", "DOCKER"
        }
        if (System.getProperty("masterIP") == null) {
            systemProperty "masterIP", "INVALID_MASTER_IP"
        }
        if (System.getProperty("imageVersion") == null) {
            systemProperty "imageVersion", "INVALID_IMAGE_VERSION"
        }
        if (System.getProperty("skipServiceInstallation") == null) {
            //skipServiceInstallation by default.
            systemProperty "skipServiceInstallation", "false"
        }
        if (System.getProperty("awsExec") == null) {
            systemProperty "awsExec", "false"
        }
        maxParallelForks = 1
        systemProperty "dockerImageRegistry", "${dockerRegistryUrl}"
        systemProperty "log.level", System.getProperty("log.level", "DEBUG")
        systemProperty "tlsEnabled", System.getProperty("tlsEnabled", "false")

    }

    task collectSystemTestLogsFromAws(type: Exec) {
        if (project.hasProperty('aws_secret_key')) {
            commandLine './aws/postTestScript.sh'
            args "$aws_access_key", "$aws_secret_key", "$aws_region", "$aws_key_name", "$cred_path", "$config_path", "$pravega_org", "$pravega_branch", "$travis_commit"
        }
    }
}

def getProjectVersion() {
    String ver = pravegaVersion
    if (grgit && ver.contains("-SNAPSHOT") && !project.hasProperty("simpleSnapshot")) {
        String versionLabel = ver.substring(0, ver.indexOf("-SNAPSHOT"))
        def count = grgit.log(includes:['HEAD']).size()
        def commitId = "${grgit.head().abbreviatedId}"
        ver = versionLabel + "-" + count + "." + commitId + "-SNAPSHOT"
    }
    return ver
}

def getDefaultJavaVersion() {
    return "11"
}

def getSystemTestClass(arg) {
    if (arg == null || arg.contains("*")) {
        return arg
    }

    def str = arg.trim()
    def separator = System.getProperty("file.separator", "/")
    if (!str.contains(separator)) {
        str = "**" +  separator + str
    }
    if (!str.endsWith('.class')) {
        str += '.class'
    }
    return str
}

def subsetSystemTests() {
    def excludedTests = System.getProperty('excludeSystemTests')
    def includedTests = System.getProperty('includeSystemTests')
    def (includes, excludes) = [[], []]
    // Let included tests take precedence if both are specified.
    if (includedTests != null && !includedTests.isEmpty()) {
        includes = includedTests.split(',').collect { getSystemTestClass(it) }
    } else if (excludedTests != null && !excludedTests.isEmpty()) {
        excludes = excludedTests.split(',').collect { getSystemTestClass(it) }
    }

    return [includes, excludes]
}


subprojects {
    task allDeps(type: DependencyReportTask) {}
}

task publishAllJars() {
    dependsOn ':authplugin:publish'
    dependsOn ':client:publish'
    dependsOn ':common:publish'
    dependsOn ':shared:publish'
    dependsOn ':shared:security'
    dependsOn ':shared:protocol:publish'
    dependsOn ':segmentstore:contracts:publish'
    dependsOn ':segmentstore:storage:publish'
    dependsOn ':segmentstore:storage:impl:publish'
    dependsOn ':segmentstore:server:publish'
    dependsOn ':segmentstore:server:host:publish'
    dependsOn ':shared:metrics:publish'
    dependsOn ':shared:controller-api:publish'
    dependsOn ':controller:publish'
    dependsOn ':standalone:publish'
    dependsOn ':cli:admin'
    dependsOn ':cli:user'
    dependsOn ':test:system:publish'
}

task sourceCopy(type: Copy) {
    from rootDir
    into 'source'
}

task sourceTar(type: Tar) {
    dependsOn 'sourceCopy'
    from  'source'
    destinationDirectory = file('sourceArtifacts')
    archiveExtension = 'tgz'
    compression = Compression.GZIP
}

task javadocs(type: Javadoc) {
    description = "Generate main pravega javadoc"

    // Include names of any project that is to be included in the javadoc distribution
    ext.projects = [':client']
    title = "Pravega API"
    destinationDir = file("${buildDir}/javadocs")

    projects.collect {
        dependsOn project(it).delombok

        source += project(it).delombok.outputDir
        classpath += project(it).sourceSets.main.output + project(it).sourceSets.main.compileClasspath
    }

    failOnError = true
    exclude "**/impl/**"
    options.addBooleanOption("Xdoclint:all,-reference", true)
}

apply plugin: 'distribution'
distributions {
    main {
        distributionBaseName = "pravega"
        contents {
            duplicatesStrategy = "exclude"
            from ("dist/conf") {
                into "conf"
            }
            from ("config") {
                into "conf"
            }
            from (project(":controller").installDist) {
                exclude "logback.xml"
            }
            from (project(":segmentstore:server:host").installDist) {
                exclude "logback.xml"
            }
            from (project(":cli:admin").installDist)
            from (project(":cli:user").installDist)
            from (project(":standalone").installDist)
            from 'LICENSE'
            from 'NOTICE'
        }
    }
    client {
        distributionBaseName = "pravega-client"
        contents {
            from { project(":shared:authplugin").configurations.runtime }
            from { project(":shared:authplugin").configurations.runtime.allArtifacts.files }
            from { project(":client").configurations.runtime }
            from { project(":client").configurations.runtime.allArtifacts.files }
            from 'LICENSE'
            from 'NOTICE'
        }
    }
    javadoc {
        distributionBaseName = "pravega-javadoc"
        contents {
            from (javadocs)
            from 'LICENSE'
            from 'NOTICE'
        }
    }
}

tasks.withType(Tar) {
    compression = Compression.GZIP
}

task preparePravegaImage(type: Copy) {
    into "${buildDir}/docker/pravega"
    from "docker/pravega"
    from (installDist) {
        into "pravega"
        exclude "**/*.bat", "**/log4j*.jar"
    }
}

task buildPravegaImage(type: DockerBuildTask, dependsOn: preparePravegaImage) {
    baseTag = pravegaBaseTag
    dockerDir = preparePravegaImage.destinationDir.absolutePath
    dockerFile = "${dockerDir}/Dockerfile"
}

task buildBookkeeperImage(type: DockerBuildTask) {
    baseTag = bookkeeperBaseTag
    dockerDir = "docker/bookkeeper"
    dockerFile = "${dockerDir}/Dockerfile"
}

task docker(dependsOn: [buildPravegaImage, buildBookkeeperImage]) {
    description = "Builds all docker images"
}

task pushPravegaImage(type: DockerPushTask) {
    // No explicit dependency on building the pravega image
    mustRunAfter buildPravegaImage
    tag = "${pravegaBaseTag}:${version}"
}

task pushBookkeeperImage(type: DockerPushTask) {
    // No explicit dependency on building the bookkeeper image
    mustRunAfter buildBookkeeperImage
    tag = "${bookkeeperBaseTag}:${version}"
}

task dockerPush(dependsOn: [pushPravegaImage, pushBookkeeperImage]) {
    description = "Push all docker images"
}

task distribution(dependsOn: [assembleDist, assembleClientDist, assembleJavadocDist]) {
    description = "Builds a distribution package"
}


/**
 * Task for building a docker image
 */
class DockerBuildTask extends Exec {
    String baseTag
    String dockerDir
    String dockerFile

    DockerBuildTask() {
        executable project.dockerExecutable
        args "build"
        args "--pull"
        args "-t", "${->baseTag}:${project.version}"
        args "-t", "${->baseTag}:latest"
        args "-f", "${->dockerFile}"
        args "${->dockerDir}"
    }
}

/**
 * Task for pushing an image, which can either push to dockerhub or to a private registry.
 * If pushing to dockerhub, you must be logged using using `docker login` before running.
 */
class DockerPushTask extends Exec {
    String tag

    DockerPushTask() {
        executable project.dockerExecutable
        args "push", "${->getRemoteTag()}"
    }

    protected void exec() {
        // Tag the image with the remote image name first
        if (project.hasProperty('dockerRegistry')) {
            project.exec {
                commandLine project.dockerExecutable, "tag", tag, getRemoteTag()
            }
        }
        super.exec()
    }

    String getRemoteTag() {
        if (project.hasProperty('dockerRegistry')) {
            return "${project.property('dockerRegistry')}/${tag}"
        }
        else {
            return tag
        }
    }
}
