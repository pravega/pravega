/**
 * Copyright (c) Dell Inc., or its subsidiaries. All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 */
plugins.withId('maven') {
    task sourcesJar(type: Jar, dependsOn: classes) {
         classifier = 'sources'
         from sourceSets.main.allSource
    }
    artifacts{
        archives sourcesJar
        archives javadocJar
        archives testJar
    }
    uploadArchives {
        repositories {
            mavenDeployer {
                // Only configure publishing if a URL was provided
                if (project.hasProperty("publishUrl")) {
                    if (publishUrl == "mavenCentral") {
                        repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                            authentication(userName: publishUsername, password: publishPassword)
                        }
                        snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                            authentication(userName: publishUsername, password: publishPassword)
                        }
                    }
                    else if (publishUrl == "jcenterSnapshot") {
                        repository(url: "https://oss.jfrog.org/artifactory/oss-snapshot-local/") {
                            if (project.hasProperty("publishUsername") && project.hasProperty("publishPassword")) {
                                authentication(userName: publishUsername, password: publishPassword)
                            }
                        }
                    }
                    else {
                        repository(url: publishUrl) {
                            // Only configure credentials if they are provided (allows publishing to the filesystem)
                            if (project.hasProperty("publishUsername") && project.hasProperty("publishPassword")) {
                                authentication(userName: publishUsername, password: publishPassword)
                            }
                        }
                    }
                }

                pom.artifactId = "pravega" + project.path.replace(':', '-')
                pom.project {
                    name "Pravega"
                    url "https://pravega.io"
                    description "Streaming Storage Platform"
                    scm {
                        url 'https://github.com/pravega/pravega/tree/master'
                        connection 'scm:git:git://github.com/pravega/pravega.git'
                        developerConnection 'scm:git:https://github.com/pravega/pravega.git'
                    }
                    licenses {
                        license {
                            name 'The Apache License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id 'fpj'
                            name 'Flavio Junqueira'
                        }
                    }
                }

                addFilter(':') {artifact, file ->
                    artifact.name.indexOf("tests") == -1
                }
                pom(':', {
                    artifactId = "pravega" + project.path.replace(':', '-')
                    version getProjectVersion()
                })

                addFilter('tests') {artifact, file ->
                    artifact.name.indexOf("segmentstore") != -1 &&
                    artifact.name.indexOf("tests") != -1
                }

                pom('tests', {
                    artifactId = "pravega" + project.path.replace(':', '-') + "-tests"
                    version getProjectVersion()
                }).withXml {
                    def dependencies  = asNode().appendNode("dependencies")
                    configurations.compile.allDependencies.each {  dep ->
                        def depNode  = dependencies.appendNode("dependency")
                        depNode.appendNode("groupId", dep.group)
                        depNode.appendNode("artifactId", dep.name)
                        depNode.appendNode("version", dep.version)
                    }
                }

                // Sign the POM when the signing plugin is present
                plugins.withId('signing') {
                    if (project.hasProperty('doSigning')) {
                        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
                    }
                }
            }
        }
    }

    install {
        repositories {
            mavenInstaller {
                pom.artifactId = "pravega" + project.path.replace(':', '-')
            }
        }
    }

    task publish(dependsOn:'uploadArchives') {
    }
}
