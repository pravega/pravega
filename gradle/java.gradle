plugins.withId('java') {
    apply plugin: 'checkstyle'
    apply plugin: 'findbugs'
    apply plugin: 'jacoco'
    apply plugin: 'signing'

    // Apply maven-publish plugin to all non-test projects
    if (!project.path.startsWith(":test")) {
        apply plugin: 'maven-publish'
    }

    compileJava {
        options.compilerArgs.addAll([
                "-Xlint:deprecation",
                "-Xlint:divzero",
                "-Xlint:empty",
                "-Xlint:fallthrough",
                "-Xlint:finally",
                "-Xlint:overrides",
                "-Xlint:path",
                "-Werror"
        ])
    }

    archivesBaseName = "pravega" + project.path.replace(':', '-')

    task sourceJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.java
    }

    task testJar(type: Jar) {
        classifier = 'tests'
        from sourceSets.test.output
    }
    artifacts { testRuntime testJar }

    tasks.withType(Test) {
        systemProperties 'logback.configurationFile' : new File(buildDir,'resources/test/logback.xml').absolutePath
        testLogging.showStandardStreams = false
        testLogging.exceptionFormat = "FULL"
        testLogging.showCauses = true
        testLogging.showExceptions = true
        testLogging.showStackTraces = true
        testLogging.events = ["PASSED", "FAILED"]
        maxParallelForks = System.properties['maxParallelForks'] ? System.properties['maxParallelForks'].toInteger() : 1
        minHeapSize = "128m"
        maxHeapSize = "512m"
    }

    dependencies {
        // The production code uses the SLF4J logging API at compile time
        compile group: 'org.slf4j', name:'slf4j-api', version: slf4jApiVersion
        testCompile group: 'junit', name:'junit', version: junitVersion
        testCompile "org.mockito:mockito-core:${mockitoVersion}"
    }
}